
"Various utilities, should probably be moved elsewhere."

s@(Sequence traits) splitPreservingEmptys: obj &count: count
"Divides the Sequence up into subsequences as delimited by the given element."
[| subSeqs keyStart keyEnd |
  keyEnd := s indexOf: obj startingAt: 0 ifAbsent: [s size].
  subSeqs := ExtensibleArray new*, (s copyFrom: 0 to: keyEnd - 1).
  [keyEnd < s size] whileTrue:
    [count isNotNil /\ [subSeqs size >= count] ifTrue:
       [subSeqs add: (s copyFrom: keyEnd to: s size - 1).
        ^ subSeqs].
     keyStart := (s indexOf: obj startingAt: keyEnd ifAbsent: [keyEnd]) + 1.
     keyEnd := s indexOf: obj startingAt: keyStart ifAbsent: [s size].
     keyStart <= keyEnd ifTrue:
       [subSeqs add: (s copyFrom: keyStart to: keyEnd - 1)]].
  subSeqs
].

"Int16 streams"

prototypes define: #Int16ReadStream &parents: {StreamProcessor}.
Int16ReadStream define: #LittleEndian &parents: {Int16ReadStream}.
Int16ReadStream define: #BigEndian &parents: {Int16ReadStream}.

"FIXME: when reading from streams check that they aren't shorter than
16 bits"

s@(Int16ReadStream LittleEndian traits) next
[| byte1 byte2 |
  byte1 := (s source next as: Integer).
  byte2 := (s source next as: Integer).
  (byte2 bitShift: 8) bitOr: byte1
].

s@(Int16ReadStream BigEndian traits) next
[| byte1 byte2 |
  byte1 := (s source next as: Integer).
  byte2 := (s source next as: Integer).
  (byte1 bitShift: 8) bitOr: byte2
].

prototypes define: #Int16WriteStream &parents: {WriteStream}.

s@(Int16WriteStream traits) on: target
[
  s target := target.
  s
].

Int16WriteStream define: #LittleEndian &parents: {Int16WriteStream}.
Int16WriteStream define: #BigEndian &parents: {Int16WriteStream}.

Int16ReadStream LittleEndian traitsWindow
        atSlotNamed: #printNameWindow
        put: 'Int16ReadStream LittleEndian'.
Int16ReadStream BigEndian traitsWindow
        atSlotNamed: #printNameWindow
        put: 'Int16ReadStream BigEndian'.
Int16WriteStream LittleEndian traitsWindow
         atSlotNamed: #printNameWindow
         put: 'Int16WriteStream LittleEndian'.
Int16WriteStream BigEndian traitsWindow
         atSlotNamed: #printNameWindow
         put: 'Int16WriteStream BigEndian'.

Int16WriteStream LittleEndian addSlot: #target.
Int16WriteStream BigEndian addSlot: #target.

s@(Int16WriteStream LittleEndian traits) nextPut: char
[
  s target nextPut: ((char bitAnd: 16r00FF) as: ASCIIString Character).
  s target nextPut: ((char bitShift: -8) as: ASCIIString Character).
].

s@(Int16WriteStream BigEndian traits) nextPut: char
[
  s target nextPut: ((char bitShift: -8) as: ASCIIString Character).
  s target nextPut: ((char bitAnd: 16r00FF) as: ASCIIString Character).
].
