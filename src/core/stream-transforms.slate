streams define: #ConcatenatedReadStream
	   &parents: {PositionableReadStream}
           &slots: {
"A Stream acting as the result of concatenating all the source streams'
sources into a single source to be read."
#sources -> ExtensibleArray new.
"The Streams which are concatenated."
#currentSource.
"The current Stream being read."
#position -> 0.
"The overall position of the stream, taken by tabulating."
}.

s1@(Stream traits) ; s2@(Stream traits)
[
  ConcatenatedReadStream newOn: {s1. s2}
].

cs@(ConcatenatedReadStream traits) ; s@(Stream traits)
[
  cs sources addLast: s.
  cs
].

s@(ConcatenatedReadStream traits) on: sources
[
  s sources: (sources as: s sources).
  s currentSource: s sources first.
  s position: 0.
  s
].

s@(ConcatenatedReadStream traits) hasAnEnd [s sources allSatisfy: #hasAnEnd `er].

s@(ConcatenatedReadStream traits) isAtEnd
[
  s currentSource = s sources last /\ [s currentSource isAtEnd]
].

s@(ConcatenatedReadStream traits) next
[
  s currentSource isAtEnd ifTrue:
    [| nextSrcIdx |
     nextSrcIdx: (s sources indexOf: s currentSource) + 1.
     nextSrcIdx = s sources size ifTrue: [^ s exhaustion].
     s currentSource: (s sources at: nextSrcIdx)].
  s currentSource next
].

streams define: #FlattenedReadStream &parents: {ReadStream} &slots: {#sources -> Stack new.}.
"Allows the use of the flatten message to create a recursive traversal of
all contained ReadStreams."

s@(Stream traits) flatten
[FlattenedReadStream newOn: s].

s@(FlattenedReadStream traits) flatten
[s].

s1@(FlattenedReadStream traits) on: s2
[
  s1 sources: (s1 sources newWith: s2).
  s1
].

s@(FlattenedReadStream traits) hasAnEnd [s sources allSatisfy: #hasAnEnd `er].

s@(FlattenedReadStream traits) isAtEnd
[s sources size = 1 /\ [s sources top isAtEnd]].

s@(FlattenedReadStream traits) next
[| obj |
  [s sources top isAtEnd]
    whileTrue:
     [s sources size <= 1
       ifTrue: [s exhausted]
       ifFalse: [s sources pop]].
  obj: s sources top next.
  obj isSource
   ifTrue: [s sources push: obj reader.
            s next]
   ifFalse: [obj]
].

"A GeneratorStream will generate a stream of values by applying a step
block to some seed value, and subsequently to what remains of the seed
value, until the end block is satisfied."
streams define: #GeneratorStream &parents: {ReadStream}
&slots: {#currentValue -> Nil.
         #step -> Method Identity.
         #end -> [| :_ | False]}.

seed@(Root traits) generate: step until: end
[GeneratorStream cloneSettingSlots: #(currentValue step end)
		 to: {seed. step. end}].

seed@(Root traits) generate: step
[seed generate: step until: GeneratorStream end].

s@(GeneratorStream traits) terminal [s step].

s@(GeneratorStream traits) hasAnEnd
"TODO: Change to `maybe`, i.e. the Halting Problem."
[True].

s@(GeneratorStream traits) isAtEnd
[s end applyWith: s currentValue].

s@(GeneratorStream traits) next
[
  s isAtEnd
    ifTrue: [s exhausted].
  s currentValue: (s step applyWith: s currentValue)
].

Stream traits define: #EchoStream &parents: {ReadWriteStream} &slots: {#original. #dribble}.
"An EchoStream wraps some original stream and duplicates any interaction,
reading or writing, done on it to another stream. This relies on EchoStream
having defined all of the stream interaction methods that the client relies
upon."
"Inspired by Henry Lieberman's 1986 paper to the first OOPSLA, titled:
_Using Prototypical Objects to Implement Shared Behavior in Object Oriented Systems_
and archived at:
http://lieber.www.media.mit.edu/people/lieber/Lieberary/OOP/Delegation/Delegation.html"
"The target for the echo'ing operation, called dribble after the Lieberman / 
Lisp terminology."

s@(Stream traits) echoTo: log
"Creates and returns a new EchoStream from the first to the log Stream."
[(s EchoStream newOn: s) `>> [dribble: log. ]].

s@(Stream traits) echo
"Creates and returns a new EchoStream to the console."
[s echoTo: Console writer].

e@(Stream EchoStream traits) on: s
[
  e original: s.
  e
].

e@(Stream EchoStream traits) echoTo: s
"Chooses another Stream to dribble to, ensuring that echoTo: is not repeated."
[
  e dribble: s.
  e
].

e@(Stream EchoStream traits) terminal [e dribble terminal].

e@(Stream EchoStream traits) next
[
  e dribble nextPut: e original next
].

e@(Stream EchoStream traits) next: n
[
  e dribble nextPutAll: (e original next: n)
].

e@(Stream EchoStream traits) nextPut: obj
[
  e dribble nextPut: (e original nextPut: obj)
].

e@(Stream EchoStream traits) nextPutAll: seq
[
  e dribble nextPutAll: (e original nextPutAll: seq)
].

e@(Stream EchoStream traits) position: n
[
  e dribble position: (e original position: n)
].

ReadStream traits define: #LineStream &parents: {StreamProcessor} &slots: {#trailingNL -> False}.
"A stream that reads lines of text from a wrapped stream, terminated by
an appropriate line-ending (\r, \n or \r\n)."

l@(ReadStream LineStream traits) on: s
[
  l source: s.
  l trailingNL: False.
  l
].

s@(Stream traits) lines [s LineStream newOn: s].

l@(ReadStream LineStream traits) collectionType [Array].

l@(ReadStream LineStream traits) nextLine
[
  [| :line |
    l source `cacheAs: #underlying.
    [underlying isAtEnd
        ifTrue: [line collection isEmpty ifTrue: [^ Nil]. False]
        ifFalse: [| char |
          (char: underlying next) caseOf: {
            $\r -> [l trailingNL: True. False].
            $\n -> [| nl | nl: l trailingNL. l trailingNL: False. nl].
          } otherwise: [line nextPut: char. True]]
    ] whileTrue.
  ] writingAs: String
].

l@(ReadStream LineStream traits) next: n putInto: seq startingAt: start
[
  0 below: n
    do: [ |:index line| 
           line: l nextLine. 
           line ifNil: [^ index] ifNotNil: [ seq at: index + start put: line]].
  n
].


WriteStream traits define: #LineStream &parents: {Stream WrapperStream}.
"A Stream that writes Strings of text to a wrapped Stream, interpolated with
an appropriate line-ending."

l@(WriteStream LineStream traits) nextPut: str@(String traits)
[l original ; str ; '\n'].

l@(WriteStream LineStream traits) nextPutAll: c
[c do: [| :each | l nextPut: each]].

l@(WriteStream LineStream traits) collectionType [Array].


ReadWriteStream traits define: #LineStream &parents:
  {ReadStream LineStream. WriteStream LineStream}.
